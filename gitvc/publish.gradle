apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'

def pomMetadata = [
        'groupId'   : project.group ?: rootProject.group,
        'artifactId': project.name,
        'version'   : project.version ?: rootProject.version,
        'desc'      : '一个自动化版本号管理Gradle插件',
        'website'   : 'https://github.com/tuuzed/gitvc-gradle-plugin'
]

/**
 * maven plugin
 */
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = pomMetadata.groupId
            pom.artifactId = pomMetadata.artifactId
            pom.version = pomMetadata.version
            repository(url: "file:/" + rootProject.file(".repo").absolutePath)
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * bintray-release plugin
 */
publish {
    bintrayUser = System.getenv('bintray.user')
    bintrayKey = System.getenv('bintray.apiKey')
    userOrg = 'tuuzed'
    repoName = 'maven'
    dryRun = false
    override = true
    groupId = pomMetadata.groupId
    artifactId = pomMetadata.artifactId
    publishVersion = pomMetadata.version
    desc = pomMetadata.desc
    website = pomMetadata.website
}